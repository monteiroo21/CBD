// NMEC: 114547

# 1. Encontre todos os atores que dirigiram um filme em que também atuaram e apresente o nome do ator e o título do filme.
MATCH(p:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(p:Person)   
RETURN p.name, m.title


# 2. Para cada filme realizado depois de 2005, apresente os nomes de todos os atores que atuaram nesse filme.
MATCH(m:Movie)<-[:ACTED_IN]-(p:Person)   
WHERE m.released > 2005   
RETURN m.title, p.name


# 3. Encontre pares de nós com mais do que uma relação entre si.
MATCH (a)-[r]->(b)   
WITH a, b, count(r) as c   
WHERE c > 1   
RETURN a, b, c as relations_count   
ORDER BY c DESC


# 4. Encontre todos os pares de pessoas que fizeram revisões do mesmo filme. Apresente os seus nomes e título de cada filme.
MATCH (p1:Person)-[:REVIEWED]->(m:Movie)<-[:REVIEWED]-(p2:Person)   
RETURN p1.name, p2.name, m.title


# 5. Encontre todos os pares de atores que atuaram em vários filmes juntos.
MATCH (p1:Person)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(p2:Person) 
WITH p1, p2, count(m) as num_movies   
WHERE num_movies > 1   
RETURN p1.name, p2.name, num_movies   
ORDER BY num_movies DESC


# 6. Determine a idade média do elenco do filme "Apollo 13" no ano do lançamento do filme.
MATCH (p1:Person)-[:ACTED_IN]->(m:Movie)   
WHERE m.title = "Apollo 13"   
RETURN m.title, avg(m.released - p1.born) as avg_age


# 7. Encontre os 10 filmes com o elenco mais velho no momento do lançamento do filme. Apresente o filme e a idade média arredondada a 2 casas decimais, por ordem decrescente.
MATCH (p1:Person)-[:ACTED_IN]->(m:Movie)   
RETURN m.title, round(avg(m.released - p1.born), 2) as avg_age   
ORDER BY avg_age DESC   
LIMIT 10 


# 8. Apresente o subgrafo ACTED_IN do filme com o elenco mais novo, no momento do lançamento do filme.
MATCH (p1:Person)-[:ACTED_IN]->(m:Movie)   
WITH m, round(avg(m.released - p1.born), 2) as avg_age   
ORDER BY avg_age ASC   
LIMIT 1   
MATCH (p2:Person)-[:ACTED_IN]->(m:Movie)   
RETURN p2, m


# 9. Qual é o caminho mais curto (usando qualquer tipo de relação) entre John Cusack e Demi Moore?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))   
WHERE p1.name = "John Cusack" AND p2.name = "Demi Moore"   
RETURN path   


# 10. Qual a dimensão caminho mais curto (usando qualquer tipo de relação) entre Keanu Reeves e Tom Cruise?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))   
WHERE p1.name = "Keanu Reeves" AND p2.name = "Tom Cruise"   
RETURN length(path) AS pathDimension 


# 11. Quais são as dimensões do caminho mais curto entre pessoas com nome Jim e pessoas com nome Kevin?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))       
WHERE p1.name CONTAINS "Jim" AND p2.name CONTAINS "Kevin"    
RETURN length(path) AS pathDimension 


# 12. Que pessoas têm uma distância 2 para Jim Cash (a distância entre duas pessoas é o comprimento do caminho mais curto entre eles)?
MATCH path = (p1:Person)-[*2]-(p2:Person)    
WHERE p1.name = "Jim Cash"     
RETURN p2.name      


# 13. Qual é a maior distância de uma pessoa para Kevin Bacon?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))   
WHERE p2.name = "Kevin Bacon" AND p1<>p2   
RETURN p1.name, length(path) AS distance   
ORDER BY distance DESC   
LIMIT 1


# 14. Qual é a maior distância entre duas pessoas?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))   
WHERE p1<>p2   
RETURN p1.name, p2.name, length(path) AS distance   
ORDER BY distance DESC   
LIMIT 1   


# 15. Qual é a distribuição de distâncias em pares (isto é, para a distância 1, 2, 3, ..., quantos pares de pessoas têm essa distância um do outro)?
MATCH path = shortestPath((p1:Person)-[*]-(p2:Person))   
WHERE p1<>p2    
RETURN length(path) AS distance, count(*) AS numRelations   
ORDER BY distance


# 16. Indique as 10 pessoas com menor distância média em que o caminho entre elas são relações do tipo ACTED_IN.
MATCH path = shortestPath((p1:Person)-[:ACTED_IN*]-(p2:Person))   
WHERE p1<>p2   
RETURN p1.name, p2.name, avg(length(path)) as avg_distance   
ORDER BY avg_distance ASC   
LIMIT 10


# 17. Apresente os atores que tiveram pelo menos uma role para personagens com o prefixo 'Dr.'. O resultado deve ser uma lista com o par “Ator, Lista das roles”.
MATCH (p1:Person)-[r:ACTED_IN]->(m:Movie)    
WHERE ANY(role in r.roles WHERE role STARTS WITH "Dr.")   
RETURN p1.name AS Ator, [role in r.roles WHERE role STARTS WITH "Dr."] AS Lista


# 18. Encontre os atores que já atuaram em filmes com o mesmo diretor, mas nunca trabalharam no mesmo filme juntos.
MATCH (p1:Person)-[:ACTED_IN]->(:Movie)<-[:DIRECTED]-(d:Person)-[:DIRECTED]->(:Movie)<-[:ACTED_IN]-(p2:Person)
WHERE NOT EXISTS((p1)-[:ACTED_IN]->(:Movie)<-[:ACTED_IN]-(p2)) AND p1 <> p2
RETURN p1.name AS Actor1, p2.name AS Actor2, d.name AS SharedDirector
ORDER BY d.name

# 19. Liste os filmes em que o mesmo ator desempenhou múltiplas personagens.
MATCH (p:Person)-[r:ACTED_IN]->(m:Movie)
WHERE SIZE(r.roles) > 1
RETURN p.name AS Actor, m.title AS Movie, r.roles AS Roles
ORDER BY SIZE(r.roles) DESC

# 20. Filme com mais reviews
MATCH (:Person)-[:REVIEWED]->(m:Movie)
RETURN m.title AS Movie, COUNT(*) AS numReviews
ORDER BY numReviews DESC
LIMIT 1